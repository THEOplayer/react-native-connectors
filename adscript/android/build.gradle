import java.util.zip.GZIPInputStream

buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : '2.2.10'

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.12.3"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  namespace = "com.theoplayer.reactnative.adscript"
  compileSdkVersion safeExtGet("THEOplayerAdScript_compileSdkVersion", 36)

  defaultConfig {
    minSdkVersion safeExtGet("THEOplayerAdScript_minSdkVersion", 21)
    targetSdkVersion safeExtGet("THEOplayerAdScript_targetSdkVersion", 36)
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
  // Local Maven repo
  maven { url("local") }
  maven { url "https://maven.theoplayer.com/releases" }
}

rootProject.allprojects {
  repositories {
    maven { url("$rootDir/../node_modules/@theoplayer/react-native-analytics-adscript/android/local") }
  }
}

// The AdScript connector requires at least THEOplayer SDK v5.11.0.
def theoplayer_sdk_version = safeExtGet('THEOplayer_sdk', '[5.11.0, 11.0.0)')
def kotlin_version = safeExtGet("THEOplayerAdScript_kotlinVersion", "2.2.10")
def adscript_version = safeExtGet("THEOplayerAdScript_adscriptVersion", "1.0.10")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.lifecycle:lifecycle-process:2.5.1'

  // THEOplayer dependencies
  compileOnly "com.theoplayer.theoplayer-sdk-android:core:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:$theoplayer_sdk_version"
  implementation project(':react-native-theoplayer')

  // TEMP: replace this once the native connector has been published
//  implementation "com.theoplayer.android-connector:adscript:$adscript_version"
  implementation files("./libs/adscript.aar")
  def aarFileName = "AdScriptApiClient_v${adscript_version}.aar"
  def adScriptSdkDir = rootProject.properties['adScriptSdkDir']
  if (!adScriptSdkDir) {
    logger.warn("⚠️ WARNING: adScriptSdkDir not set.")
  } else if (!rootProject.file(adScriptSdkDir).exists()) {
    logger.warn("⚠️ WARNING: adScriptSdkDir does not exist at: ${adScriptSdkDir}")
  } else {
    def aarFile = new File(adScriptSdkDir, aarFileName)

    // The AdScript aar file is typically provided as .gz, check if we still need to extract.
    def gzFile = new File("${aarFile}.gz")
    if (!aarFile.exists() && gzFile.exists()) {
      new FileInputStream(gzFile).withCloseable { fis ->
        new GZIPInputStream(fis).withCloseable { gis ->
          new FileOutputStream(aarFile).withCloseable { fos ->
            fos << gis
          }
        }
      }
    }
    dependencies {
      implementation fileTree(dir: "${rootProject.file(adScriptSdkDir)}/$aarFileName", include: ['*.aar', '*.jar'], exclude: [])
    }
  }

  // Align the Kotlin SDK libraries with the same version.
  implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version"))
}
