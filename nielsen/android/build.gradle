buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : '1.9.10'

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet("THEOplayerNielsen_compileSdkVersion", 34)

  defaultConfig {
    minSdkVersion safeExtGet("THEOplayerNielsen_minSdkVersion", 21)
    targetSdkVersion safeExtGet("THEOplayerNielsen_targetSdkVersion", 34)
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
  // Local Maven repo
  maven { url("local") }
  maven { url 'https://jitpack.io' }
}

rootProject.allprojects {
  repositories {
    maven { url 'https://raw.githubusercontent.com/NielsenDigitalSDK/nielsenappsdk-android/master/' }
  }
}

// The Nielsen connector requires at least THEOplayer SDK v5.11.0.
def theoplayer_sdk_version = safeExtGet('THEOplayer_sdk', '[5.11.0, 9.0.0)')
def kotlin_version = safeExtGet("THEOplayerNielsen_kotlinVersion", "1.9.10")
def nielsen_version = safeExtGet("THEOplayerNielsen_nielsenVersion", "9.2.0.0")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.lifecycle:lifecycle-process:2.5.1'

  // THEOplayer dependencies
  compileOnly "com.theoplayer.theoplayer-sdk-android:core:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:$theoplayer_sdk_version"
  implementation project(':react-native-theoplayer')

  // Local maven dependency: remove once published.
  implementation "com.theoplayer.android-connector:nielsen:$theoplayer_sdk_version"

  implementation "com.nielsenappsdk.global:ad:$nielsen_version"

  // Align the Kotlin SDK libraries with the same version.
  implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version"))
}
