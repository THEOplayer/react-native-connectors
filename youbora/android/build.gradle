buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : '2.2.10'

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.12.3"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet("THEOplayerYoubora_compileSdkVersion", 36)

  defaultConfig {
    minSdkVersion safeExtGet("THEOplayerYoubora_minSdkVersion", 23)
    targetSdkVersion safeExtGet("THEOplayerYoubora_targetSdkVersion", 36)
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
  // Local Maven repo
  maven { url("local") }
  maven { url "https://maven.theoplayer.com/releases" }
}

rootProject.allprojects {
  repositories {
    maven { url "https://artifact.plugin.npaw.com/artifactory/plugins/android" }
  }
}

def theoplayer_sdk_version = safeExtGet('THEOplayer_sdk', '[10.0.0, 11.0.0)')
def kotlin_version = safeExtGet("THEOplayerYoubora_kotlinVersion", "2.2.10")
def youbora_version = safeExtGet("THEOplayerYoubora_youboraVersion", "7.3.12")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.lifecycle:lifecycle-process:2.5.1'

  // THEOplayer dependencies
  compileOnly "com.theoplayer.theoplayer-sdk-android:core:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:$theoplayer_sdk_version"
  implementation project(':react-native-theoplayer')

  implementation "com.npaw.plugin:plugin:$youbora_version"
  implementation "com.npaw.theo:plugin-theo:$youbora_version"

  // Align the Kotlin SDK libraries with the same version.
  implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version"))
}
