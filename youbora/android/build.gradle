buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") :
    project.properties["THEOplayerYoubora_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet("THEOplayerYoubora_compileSdkVersion", 31)

  defaultConfig {
    minSdkVersion safeExtGet("THEOplayerYoubora_minSdkVersion", 21)
    targetSdkVersion safeExtGet("THEOplayerYoubora_targetSdkVersion", 31)
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
  // Local Maven repo
  maven { url("local") }
  maven { url 'https://jitpack.io' }
}

rootProject.allprojects {
  repositories {
    maven { url "https://npaw.jfrog.io/artifactory/youbora/" }
    maven { url("$rootDir/../node_modules/@theoplayer/react-native-analytics-youbora/android/local") }
  }
}

def theoplayer_sdk_version = safeExtGet('THEOplayer_sdk', '[5.10.0,6.0)')
def kotlin_version = safeExtGet("THEOplayerYoubora_kotlinVersion", "1.8.10")
def youbora_version = safeExtGet("THEOplayerYoubora_youboraVersion", "+")
def youbora_config_version = safeExtGet("THEOplayerYoubora_youboraConfigVersion", "0.4.7")
def enabledV4 = theoplayer_sdk_version.toString().startsWith("4.")
def core_prefix = enabledV4 ? 'unified' : 'core'
def integration_prefix = enabledV4 ? 'unified' : 'integration'

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.lifecycle:lifecycle-process:2.5.1'

  // THEOplayer dependencies
  compileOnly "com.theoplayer.theoplayer-sdk-android:$core_prefix:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:$integration_prefix-ads:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:$integration_prefix-ads-ima:$theoplayer_sdk_version"
  compileOnly "com.theoplayer.theoplayer-sdk-android:$integration_prefix-ads-ima:$theoplayer_sdk_version"
  implementation project(':react-native-theoplayer')

  implementation "com.nicepeopleatwork:theoplayer-unified-adapter:$youbora_version"
  implementation "com.nicepeopleatwork:configutil:$youbora_config_version"

  // Align the Kotlin SDK libraries with the same version.
  implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version"))
}
