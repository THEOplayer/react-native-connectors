buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") :
    project.properties["THEOplayerComscore_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet("THEOplayerComscore_compileSdkVersion", 31)

  defaultConfig {
    minSdkVersion safeExtGet("THEOplayerComscore_minSdkVersion", 21)
    targetSdkVersion safeExtGet("THEOplayerComscore_targetSdkVersion", 31)
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
  // Local Maven repo
  maven { url("local") }
  maven { url 'https://jitpack.io' }
  maven {
    url "https://comscore.bintray.com/Analytics"
  }
}

def theoplayer_version = safeExtGet('THEOplayer_sdk', '+')
def kotlin_version = safeExtGet("THEOplayerComscore_kotlinVersion", "1.8.10")
def comscore_version = safeExtGet("THEOplayerComscore_comscoreVersion", "6.+")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation "com.theoplayer.theoplayer-sdk-android:unified:$theoplayer_version"
  implementation "com.theoplayer.theoplayer-sdk-android:unified-ads:$theoplayer_version"
  implementation "com.theoplayer.theoplayer-sdk-android:unified-ads-ima:$theoplayer_version"
  implementation project(':react-native-theoplayer')

  implementation "com.comscore:android-analytics:$comscore_version"

  // Align the Kotlin SDK libraries with the same version.
  implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version"))
}
